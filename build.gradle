buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.9'
	id "net.minecraftforge.gradle.forge" version "2.0.2"
}

apply plugin: 'maven-publish'

def username = "${mod_name}"
if (project.hasProperty('dev_username'))
    username = "${dev_username}"

def build_number = 'CUSTOM'
if (System.getenv('BUILD_NUMBER') != null)
    build_number = System.getenv('BUILD_NUMBER')

group = package_group
archivesBaseName = mod_name
version = "${mc_version}-${mod_version}-${build_number}"

repositories {
    // JEI
    maven { url "http://dvs1.progwml6.com/files/maven" }

    // Baubles
    ivy {
        name "Asie Dependency Mirror"
        artifactPattern "http://asie.pl/javadeps/[module]-[revision](-[classifier]).[ext]"
    }
}

dependencies {
    // Prog Maven
    deobfProvided "mezz.jei:jei_1.12:4.7.2.77:api"
    runtime "mezz.jei:jei_1.12:4.7.2.77"

    // Asie Dependency Mirror
    deobfCompile name: "Baubles", version: "1.12-1.4.5"
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    replaceIn "Embers.java"
    replace "@VERSION@", project.version

    clientRunArgs += "--username=${username}"

    if (project.hasProperty('mappings_version'))
        mappings = project.mappings_version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

tasks.build.dependsOn sourceJar

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

static String getChangelogText() {
    def changelogFile = new File('changelog.txt')
    if (!changelogFile.exists())
        return ""
    String str = ''
    String separator = '---'
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (lineCount < 3) {
            lineCount++
            if (it.startsWith(separator)) {
                return
            }
        }
        if (!it.startsWith(separator)) {
            str += "$it" + (lineCount < 3 ? ':\n\n' : '\n')
            return
        }
        done = true // once we go past the first version block, parse no more
    }
    return str
}

curseforge {
    if (project.hasProperty('curse_key_TehNut'))
        apiKey = project.curse_key_TehNut

    project {
        id = "${curse_id}"
		changelog = getChangelogText()
        releaseType = 'beta'

        addArtifact sourceJar
    }
}